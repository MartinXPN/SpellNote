apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'realm-android'

repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

android {
    compileSdkVersion 28
    buildToolsVersion "29.0.0"

    defaultConfig {
        applicationId 'com.xpn.spellnote'
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 20
        versionName '2.0.8'
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/proguard/androidx-annotations.pro'
        exclude 'META-INF/androidx.exifinterface_exifinterface.version'
    }

    splits {
        abi {
            enable true             // Enables building multiple APKs per ABI.
            reset()                 // Resets the list of ABIs that Gradle should create APKs for to none.
            include "mips", "x86", "x86_64", "arm64-v8a", "armeabi-v7a"
            universalApk false      // No universal APK that includes all ABIs
        }
    }
}

// Map for the version code that gives each ABI a value.
ext.abiCodes = [mips: 1, 'armeabi-v7a': 2, x86: 3, x86_64: 4, 'arm64-v8a': 5]

import com.android.build.OutputFile

// versionCode = ext.abiCodes * 1000 + variant.versionCode (variant.versionCode = defaultConfig.versionCode)
android.applicationVariants.all { variant ->

    // Assigns a different version code for each output APK other than the universal APK.
    variant.outputs.each { output ->

        // Stores the value of ext.abiCodes that is associated with the ABI for this variant.
        def baseAbiVersionCode = project.ext.abiCodes.get(output.getFilter(OutputFile.ABI))

        // apk not in ext.abiCodes (universal) => map lowest possible version code
        if (baseAbiVersionCode != null) {
            output.versionCodeOverride = baseAbiVersionCode * 1000 + variant.versionCode
        }
    }
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'

    // Android Support libraries
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.vectordrawable:vectordrawable-animated:1.0.0'
    implementation 'androidx.exifinterface:exifinterface:1.0.0'
    implementation 'androidx.browser:browser:1.0.0'
    implementation 'androidx.vectordrawable:vectordrawable-animated:1.0.0'
    implementation 'androidx.exifinterface:exifinterface:1.0.0'

    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.0'

    /// Firebase
    implementation 'com.google.firebase:firebase-core:17.2.0'       // Analytics
    implementation 'com.google.firebase:firebase-ads:18.2.0'        // Ads
    implementation 'com.google.firebase:firebase-storage:19.0.0'    // Storage
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1' // Crashlytics
    implementation 'com.google.firebase:firebase-ml-vision:23.0.0'  // OCR - text recognition
    implementation 'com.google.firebase:firebase-perf:19.0.0'       // Performance

    implementation 'com.anjlab.android.iab.v3:library:1.0.44'       // Billing
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'           // RxAndroid
    implementation 'io.reactivex.rxjava2:rxjava:2.2.12'             // RxJAVA
    implementation 'com.squareup.retrofit2:retrofit:2.6.1'          // Retrofit for HTTP requests
    implementation 'com.squareup.retrofit2:converter-gson:2.6.1'    // Retrofit Gson for parsing json
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.6.1'   // Retrofit adapter for Rx
    implementation 'com.jakewharton.timber:timber:4.7.1'            // Logger
    implementation 'com.annimon:stream:1.2.1'                       // Stream API
    implementation 'org.apache.commons:commons-lang3:3.9'           // Apache Commons: Utilities

    implementation 'com.otaliastudios:cameraview:1.6.0'             // Camera View
    implementation 'de.hdodenhof:circleimageview:3.0.0'             // Circle ImageView
    implementation 'com.daimajia.swipelayout:library:1.2.0@aar'     // Swipe layout (swipe items in document list)
    implementation 'com.github.vihtarb:tooltip:0.2.0'               // Popups for tutorial

    implementation 'com.caverock:androidsvg:1.4'                    // SVG for Android
    implementation 'com.github.bumptech.glide:glide:4.9.0'          // Glide - Image loader
    annotationProcessor 'androidx.annotation:annotation:1.1.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.9.0'
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'
apply plugin: 'com.google.firebase.firebase-perf'
